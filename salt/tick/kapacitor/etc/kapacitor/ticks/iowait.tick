// Parameters
var info = 30
var warn = 60
var crit = 90

var period = 1m
var every = 1m

var suppress = 5m

var db = 'telegraf'
var rp = 'autogen'

var linux = stream
  |from()
    .database(db)
    .retentionPolicy(rp)
    .measurement('cpu')
    .where(lambda: "cpu" == 'cpu-total')
    .where(lambda: "team" != 'yellow' )
    .groupBy('host')
  |window()
    .period(period)
    .every(every)
  |mean('usage_iowait')
    .as('stat')

linux
  |alert()
    .id('[kapacitor] {{ index .Tags "host"}}')
    .message('{{ .ID }} CPU iowait is {{ .Level }} value: {{ index .Fields "stat"}}')
    .info(lambda: "stat" > info)
    .warn(lambda: "stat" > warn)
    .crit(lambda: "stat" > crit)
    .alerta()
      .value('{{ index .Fields "stat" }}')
      .resource('{{ index .Tags "host"}}')
  |log()
